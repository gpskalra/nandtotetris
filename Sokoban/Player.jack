/**
 * Implements a player for the Sokoban Game.
 *
 * A player is represented by a small circle with diameter
 * 16 pixels on the screen.
 */
class Player {

    // The grid location
    // 0 <= x < 16
    // 0 <= y < 8
    field int x;
    field int y;
    
    /**
     * Constructs a new player with the given (x,y) grid
     * coordinates.
     */
    constructor Player new(int gridX,int gridY) {
       let x = gridX;
       let y = gridY;
       do draw();
       return this;
    }

    /** Deallocates the object's memory. */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    /** Getter for x coordinate. */
    method int getX() {
        return x;
    }

    /** Getter for y coordinate. */
    method int getY() {
        return y;
    }

    /** Draws the player on the screen */
    method void draw() {
        do Screen.setColor(true);
        do Screen.drawCircle((32*x) + 16,(32*y) + 16
        ,8);
        return;
    }

    /** Erases the player from the screen. */
    method void erase() {
        do Screen.setColor(false);
        do Screen.drawRectangle(32*x,32*y,(32*x) + 31,
                                (32*y) + 31);
        return;
    }

    /** Moves the player one step in the given direction. */
    method void move(int dirX,int dirY) {
        do erase();
        let x = x + dirX;
        let y = y + dirY;
        do draw();
        return;
    }
}
